// tag::MetametaModel[]
language PiLanguage

root concept PiLanguageUnit {
    name: string;
    @part concepts: PiLangConceptReference[];
    @part enumerations: PiLangEnumeration[];
    @part unions: PiLangUnion[];
}

abstract concept PiLangElement {
    name: string
}

concept PiLangEnumeration base PiLangElement {
    name: string;
    @part literals: string[];
}

concept PiLangUnion base PiLangElement {
    name: string;
    @reference parts: PiLangConceptReference[];
}

concept PiLangConceptReference {
    readonly language: PiLanguageUnit;
    name: string;
}

concept PiLangConcept base PiLangElement {
    //readonly language: PiLanguageUnit;
    name: string;
    isAbstract: boolean;
    isRoot:boolean;

    isExpression: boolean;
    isBinaryExpression: boolean;
    isExpressionPlaceHolder: boolean;
    symbol?: string;
    priority?: number;

    @part properties: PiLangPrimitiveProperty[];
    @part parts: PiLangConceptProperty[];
    @part references: PiLangConceptProperty[];

    @reference base?: PiLangConceptReference;

    @reference left?: PiLangConceptReference;
    @reference right?: PiLangConceptReference;
}

abstract concept PiLangProperty {
    name: string;
    isList: boolean;
}

concept PiLangPrimitiveProperty base PiLangProperty {
    name: string;
    isStatic: boolean;
    initialValue?: string;
    type: string;
    @reference owningConcept: PiLangConceptReference;
}

concept PiLangConceptProperty base PiLangProperty {
    name: string;
    @reference type: PiLangConceptReference;
    @reference owningConcept: PiLangConceptReference;
}

// end::MetametaModel[]