language Demo

root concept DemoModel {
    name: string
    @part entities: DemoEntity[]
    @part functions: DemoFunction[]
}

concept DemoEntity {
    name: string
    @part attributes: DemoAttribute[]
    @part functions: DemoFunction[]
    @editor trigger: string = "entity"
}

concept DemoAttribute {
    name: string
    declaredType: enum DemoAttributeType
    @editor trigger: string = "attribute"
}

concept DemoFunction {
    name: string
    declaredType : enum DemoAttributeType
    @part expression: DemoExpression
    @part parameters: DemoVariable[]
    @editor trigger: string = "function"
}

concept DemoVariable {
    name: string
    declaredType: enum DemoAttributeType
    @editor trigger: string = "var"
}

abstract expression concept DemoExpression {}

expression placeholder concept DemoPlaceholderExpression base DemoExpression {}

abstract expression concept DemoLiteralExpression base DemoExpression {}

expression concept DemoStringLiteralExpression base DemoLiteralExpression {
    value: string
    @editor trigger: string = "'"
}

expression concept DemoNumberLiteralExpression base DemoLiteralExpression {
    value: string
    @editor trigger: regexp = "[0-9]"
}

expression concept DemoBooleanLiteralExpression base DemoLiteralExpression {
    value: string
    @editor trigger: regexp = "[TF]"
}

expression concept DemoAbsExpression base DemoExpression {
    @part expr: DemoExpression
    @editor trigger: string = "abs"
}

abstract binary expression concept DemoBinaryExpression base DemoExpression {
    @part left: DemoExpression
    @part right: DemoExpression
    @editor symbol: string = "unknown"
}

binary expression concept DemoMultiplyExpression base DemoBinaryExpression {
    @editor symbol: string = "*"
    @editor priority: number = "8"
}

binary expression concept DemoPlusExpression base DemoBinaryExpression {
    @editor symbol: string = "+"
    @editor priority: number = "4"
}

binary expression concept DemoDivideExpression base DemoBinaryExpression {
    @editor symbol: string = "/"
    @editor priority: number = "8"
}

binary expression concept DemoAndExpression base DemoBinaryExpression {
    @editor symbol: string = "and"
    @editor priority: number = "1"
}

binary expression concept DemoOrExpression base DemoBinaryExpression {
    @editor symbol: string = "or"
    @editor priority: number = "1"
}

abstract binary expression concept DemoComparisonExpression base DemoBinaryExpression {
}

binary expression concept DemoLessThenExpression base DemoComparisonExpression {
    @editor symbol: string = "<"
    @editor priority: number = "10"
}

binary expression concept DemoGreaterThenExpression base DemoComparisonExpression {
    @editor symbol: string = ">"
    @editor priority: number = "10"
}

binary expression concept DemoEqualsExpression base DemoComparisonExpression {
    @editor symbol: string = "="
    @editor priority: number = "10"
}

expression concept DemoFunctionCallExpression base DemoExpression {
    @reference functionDefinition: DemoFunction
    @editor trigger: string = "F"
}

expression concept DemoIfExpression base DemoExpression {
    @part condition: DemoExpression
    @part whenTrue: DemoExpression
    @part whenFalse: DemoExpression
    @editor trigger : string = "if"
}

expression concept DemoVariableRef base DemoExpression {
    referredName: string
    attribute: string
}

enumeration DemoAttributeType {
    String
    Integer
    Boolean
}

union DemoType {
    DemoEntity
    DemoAttributeType
}

