 language LIonCoreM3

model LIonCoreM3 {
    name: identifier;
    units: Language[];
}

interface INamed {
    name: identifier;
}

interface NamespaceProvider {
}

modelunit Language implements INamed, NamespaceProvider {
    key: string;
    version: string;
    elements: LanguageElement[];
    reference dependsOn: Language[];
    // Freon specific addition
    file-extension = "lmm";
}

abstract concept LanguageElement base NamespacedEntity {
}

concept Property base Feature {
    reference type: Datatype;
 }

abstract concept NamespacedEntity implements INamed {
    key: string;
}

abstract concept FeaturesContainer base LanguageElement implements NamespaceProvider {
    features: Feature[];
}

abstract concept Feature base NamespacedEntity {
  optional: boolean;
}

concept Concept base FeaturesContainer {
    abstract: boolean;
    partition: boolean;
    reference extends?: Concept;
    reference implements: ConceptInterface[];
}

abstract concept Link base Feature  {
    multiple: boolean;
    reference type: FeaturesContainer;
}

concept ConceptInterface  base FeaturesContainer {
    reference extends: ConceptInterface[];
}

concept Containment  base Link { }

concept Reference base Link {
 }

abstract concept Datatype base LanguageElement {
}

limited PrimitiveType base Datatype {
    Boolean;
    String;
    Integer;
    JSON;
}

concept Enumeration base Datatype implements NamespaceProvider {
    literals: EnumerationLiteral[];
}

concept EnumerationLiteral base NamespacedEntity {
}

